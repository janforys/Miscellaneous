0  BEGIN PGM DENKO-OTW-BOKI MM 
1  CALL PGM TNC:\REF
2  CALL PGM TNC:\Z19
3  CYCL DEF 247 USTAWIENIE PKT.BAZ ~
    Q339=+2    ;NR PKT BAZOWEGO
4  BLK FORM 0.1 Z X-60 Y-60 Z-50
5  BLK FORM 0.2  X+60  Y+60  Z+1
6  STOP
7  TOOL CALL 1 Z S50
8  TCH PROBE 409 PKT BAZ.SR.MOSTKA ~
    Q321=+0    ;SRODEK W 1-SZEJ OSI ~
    Q322=+40   ;SRODEK W 2-SZEJ OSI ~
    Q311=+119  ;SZEROKOSC MOSTKA ~
    Q272=+1    ;OS POMIAROWA ~
    Q261=-45   ;WYSOKOSC POMIARU ~
    Q320=+10   ;BEZPIECZNA WYSOKOSC ~
    Q260=+100  ;BEZPIECZNA WYSOKOSC ~
    Q305=+2    ;NR W TABELI ~
    Q405=+0    ;PUNKT ODNIESIENIA ~
    Q303=+1    ;PRZEKAZ DANYCH POM. ~
    Q381=+0    ;PROBKOW. NA OSI TS ~
    Q382=+0    ;1.WSPOL. DLA OSI TS ~
    Q383=+0    ;2.WSPOLRZ.DLA OSI TS ~
    Q384=+0    ;3. WSPOL. DLA OSI TS ~
    Q333=+0    ;PUNKT BAZOWY
9  TCH PROBE 419 PKT.BAZOW.POJED. OSI ~
    Q263=+0    ;1.PKT POMIAROW 1.OSI ~
    Q264=+0    ;1.PKT 2.OSI ~
    Q261=-12   ;WYSOKOSC POMIARU ~
    Q320=+10   ;BEZPIECZNA WYSOKOSC ~
    Q260=+100  ;BEZPIECZNA WYSOKOSC ~
    Q272=+2    ;OS POMIAROWA ~
    Q267=+1    ;KIERUNEK RUCHU ~
    Q305=+2    ;NR W TABELI ~
    Q333=+0    ;PUNKT BAZOWY ~
    Q303=+1    ;PRZEKAZ DANYCH POM.
10 CYCL DEF 247 USTAWIENIE PKT.BAZ ~
    Q339=+2    ;NR PKT BAZOWEGO
11 STOP
12 * - NAW-8
13 TOOL CALL 8 Z S1500 F120
14 CYCL DEF 200 WIERCENIE ~
    Q200=+2    ;BEZPIECZNA WYSOKOSC ~
    Q201=-3.1  ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q202=+5    ;GLEBOKOSC DOSUWU ~
    Q210=+0    ;PRZER. CZAS.NA GORZE ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q211=+1    ;PRZERWA CZAS. DNIE ~
    Q395=+0    ;REFERENCJA GLEB.
15 CALL LBL 1
16 CALL LBL 2
17 M5 M9
18 * - W-5-HSS
19 TOOL CALL 15 Z S1600 F120
20 CYCL DEF 200 WIERCENIE ~
    Q200=+2    ;BEZPIECZNA WYSOKOSC ~
    Q201=-18.5 ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q202=+3    ;GLEBOKOSC DOSUWU ~
    Q210=+0    ;PRZER. CZAS.NA GORZE ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q211=+0    ;PRZERWA CZAS. DNIE ~
    Q395=+0    ;REFERENCJA GLEB.
21 CALL LBL 1
22 M5 M9
23 * - W-5.8-HSS
24 TOOL CALL 10 Z S1444 F120
25 CYCL DEF 200 WIERCENIE ~
    Q200=+2    ;BEZPIECZNA WYSOKOSC ~
    Q201=-13.5 ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q202=+3    ;GLEBOKOSC DOSUWU ~
    Q210=+0    ;PRZER. CZAS.NA GORZE ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q211=+0    ;PRZERWA CZAS. DNIE ~
    Q395=+0    ;REFERENCJA GLEB.
26 CALL LBL 2
27 M5 M9
28 * - GW-M6
29 TOOL CALL 16 Z S800
30 CYCL DEF 207 GWINTOWANIE GS ~
    Q200=+2    ;BEZPIECZNA WYSOKOSC ~
    Q201=-16   ;GLEBOKOSC GWINTU ~
    Q239=+1    ;SKOK GWINTU ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS.
31 CALL LBL 1
32 M5 M9
33 * - R-6
34 TOOL CALL 36 Z S150 F40
35 CYCL DEF 201 ROZWIERCANIE ~
    Q200=+2    ;BEZPIECZNA WYSOKOSC ~
    Q201=-12   ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q211=+0    ;PRZERWA CZAS. DNIE ~
    Q208=+400  ;POSUW RUCHU POWROTN. ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS.
36 CALL LBL 2
37 M5 M9
38 CALL PGM TNC:\REF3
39 CALL PGM TNC:\Z19
40 M30
41 * - **************
42 * - LBL 1 - M6
43 LBL 1
44 L  X-13  Y+66.5 R0 FMAX M99 M13
45 L  X+13  Y+66.5 R0 FMAX M99 M13
46 LBL 0
47 * - LBL 2 - 6H7
48 LBL 2
49 L  X-13  Y+54 R0 FMAX M99 M13
50 L  X+13  Y+54 R0 FMAX M99 M13
51 LBL 0
52 * - **************
53 END PGM DENKO-OTW-BOKI MM 
