0  BEGIN PGM KS-N100-02-2 MM 
1  BLK FORM 0.1 Z  X-60  Y-60  Z-60
2  BLK FORM 0.2  X+60  Y+60  Z+0
3  ;
4  ;===================================
5  CYCL DEF 247 USTAWIENIE PKT.BAZ ~
    Q339=+1    ;NR PKT BAZOWEGO
6  STOP
7  TOOL CALL 30 Z S50
8  TOOL DEF 26
9  ;
10 TCH PROBE 412 PKT.BAZ.OKRAG WEWN. ~
    Q321=+0    ;SRODEK W 1-SZEJ OSI ~
    Q322=+0    ;SRODEK W 2-SZEJ OSI ~
    Q262=+47   ;SREDNICA NOMINALNA ~
    Q325=+0    ;KAT POCZATKOWY ~
    Q247=+90   ;KATOWY PRZYROST-KROK ~
    Q261=-5    ;WYSOKOSC POMIARU ~
    Q320=+2    ;BEZPIECZNA WYSOKOSC ~
    Q260=+50   ;BEZPIECZNA WYSOKOSC ~
    Q301=+0    ;ODJAZD NA BEZP.WYS. ~
    Q305=+1    ;NR W TABELI ~
    Q331=+0    ;PUNKT ODNIESIENIA ~
    Q332=+0    ;PUNKT ODNIESIENIA ~
    Q303=+1    ;PRZEKAZ DANYCH POM. ~
    Q381=+0    ;PROBKOW. NA OSI TS ~
    Q382=+0    ;1.WSPOL. DLA OSI TS ~
    Q383=-36   ;2.WSPOLRZ.DLA OSI TS ~
    Q384=+0    ;3. WSPOL. DLA OSI TS ~
    Q333=+0    ;PUNKT ODNIESIENIA ~
    Q423=+4    ;LICZBA PKT POMIAR. ~
    Q365=+1    ;RODZAJ PRZEMIESZCZ.
11 ;
12 CYCL DEF 247 USTAWIENIE PKT.BAZ ~
    Q339=+1    ;NR PKT BAZOWEGO
13 ;===================================
14 ;
15 * - NAWIERT
16 TOOL CALL 26 Z S1000 F100
17 TOOL DEF 3
18 M13
19 CYCL DEF 240 NAKIELKOWANIE ~
    Q200=+2    ;BEZPIECZNA WYSOKOSC ~
    Q343=+0    ;WYBOR SRED./GLEBOK. ~
    Q201=-3.3  ;GLEBOKOSC ~
    Q344=-10   ;SREDNICA ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q211=+0    ;PRZERWA CZAS. DNIE ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS.
20 CALL LBL 1
21 ;
22 CYCL DEF 240 NAKIELKOWANIE ~
    Q200=+2    ;BEZPIECZNA WYSOKOSC ~
    Q343=+0    ;WYBOR SRED./GLEBOK. ~
    Q201=-4.9  ;GLEBOKOSC ~
    Q344=-10   ;SREDNICA ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q211=+0    ;PRZERWA CZAS. DNIE ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS.
23 CALL LBL 2
24 CYCL DEF 240 NAKIELKOWANIE ~
    Q200=+2    ;BEZPIECZNA WYSOKOSC ~
    Q343=+0    ;WYBOR SRED./GLEBOK. ~
    Q201=-5.55 ;GLEBOKOSC ~
    Q344=-10   ;SREDNICA ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q211=+0    ;PRZERWA CZAS. DNIE ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS.
25 CALL LBL 3
26 M5 M9
27 ;
/ 28 STOP
29 * - W6.3
30 TOOL CALL 3 Z S1300 F120
31 TOOL DEF 27
32 M3 M7
33 CYCL DEF 200 WIERCENIE ~
    Q200=+2    ;BEZPIECZNA WYSOKOSC ~
    Q201=-19   ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q202=+4    ;GLEBOKOSC DOSUWU ~
    Q210=+0    ;PRZER. CZAS.NA GORZE ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q211=+0    ;PRZERWA CZAS. DNIE ~
    Q395=+0    ;REFERENCJA GLEB.
34 CALL LBL 1
35 M5 M9
36 ;
37 * - EG M6
38 TOOL CALL 27 Z S150
39 TOOL DEF 7
40 M13
41 CYCL DEF 207 GWINTOWANIE GS  ~
    Q200=+4    ;BEZPIECZNA WYSOKOSC ~
    Q201=-16.5 ;GLEBOKOSC GWINTU ~
    Q239=+1    ;SKOK GWINTU ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS.
42 CALL LBL 1
43 ;
44 * - W3.3
45 TOOL CALL 7 Z S2450 F120
46 TOOL DEF 22
47 M3 M7
48 CYCL DEF 200 WIERCENIE ~
    Q200=+2    ;BEZPIECZNA WYSOKOSC ~
    Q201=-18   ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q202=+3    ;GLEBOKOSC DOSUWU ~
    Q210=+0    ;PRZER. CZAS.NA GORZE ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q211=+0    ;PRZERWA CZAS. DNIE ~
    Q395=+0    ;REFERENCJA GLEB.
49 CALL LBL 2
50 M5 M9
51 ;
52 * - M4
53 TOOL CALL 22 Z S100
54 TOOL DEF 23
55 M13
56 CYCL DEF 207 GWINTOWANIE GS  ~
    Q200=+4    ;BEZPIECZNA WYSOKOSC ~
    Q201=-15   ;GLEBOKOSC GWINTU ~
    Q239=+0.7  ;SKOK GWINTU ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS.
57 CALL LBL 2
58 ;
59 * - FI11
60 TOOL CALL 23 Z S7988 F1500
61 TOOL DEF 30
62 M7 M3
63 CYCL DEF 208 SPIRALNE FREZ. OTW. ~
    Q200=+1    ;BEZPIECZNA WYSOKOSC ~
    Q201=-12   ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q334=+0.8  ;GLEBOKOSC DOSUWU ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q335=+11   ;SREDNICA NOMINALNA ~
    Q342=+0    ;WYW.WSTEP. SREDNICA ~
    Q351=+1    ;RODZAJ FREZOWANIA
64 CALL LBL 3
65 M5 M9
66 ;
67 L  Z+0 R0 FMAX M91
68 L  X+260  Y+535 R0 FMAX M91
69 ;
70 L M30
71 ;
72 * - LBL1 M6
73 LBL 1 ;M6
74 L  X+0  Y+36 FMAX M99
75 L  X+0  Y-36 FMAX M99
76 LBL 0
77 ;
78 * - LBL2 M4
79 LBL 2 ;M4
80 L  X+28.5  Y+0 FMAX M99
81 LBL 0
82 ;
83 * - LBL3 6.6/11
84 LBL 3
85 M3
86 L  X+34.4  Y+25 R0 FMAX M99
87 L  X-13.1  Y+40.4 R0 FMAX M99
88 L  X-42.5  Y+0 R0 FMAX M99
89 L  X-13.1  Y-40.4 R0 FMAX M99
90 L  X+34.4  Y-25 R0 FMAX M99
91 LBL 0
92 END PGM KS-N100-02-2 MM 
