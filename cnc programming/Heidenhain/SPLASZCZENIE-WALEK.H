0  BEGIN PGM SPLASZCZENIE-WALEK MM 
1  BLK FORM 0.1 Z  X-49.5  Y-100  Z-36
2  BLK FORM 0.2  X+49.5  Y+100  Z+36
3  ;-------------------------------------------
4  ;
5  ;-------------------------------------------
6  ; USTAW BAZE W OSI Y DOKLADNIE PO SRODKU SPLASZCZENIA ( SRODEK MOSTKA )
7  ; USTAW "Z" W OSI DETALU
8  ; USTAW BAZE W OSI X NA SRODKU DETALU
9  ; WYSUNIECIE NARZEDZIA - 82mm
10 ;-------------------------------------------
11 STOP
12 CYCL DEF 247 USTAWIENIE PKT.BAZ ~
    Q339=+1    ;NR PKT BAZOWEGO
13 L  X+0  Y+0 R0 FMAX
14 * - SONDA
/ 15 TOOL CALL 30 Z S50
/ 16 TOOL DEF 16
/ 17 TCH PROBE 409 PKT BAZ.SR.MOSTKA ~
    Q321=+50   ;SRODEK W 1-SZEJ OSI ~
    Q322=+50   ;SRODEK W 2-SZEJ OSI ~
    Q311=+15   ;SZEROKOSC MOSTKA ~
    Q272=+1    ;OS POMIAROWA ~
    Q261=+0    ;WYSOKOSC POMIARU ~
    Q320=+0    ;BEZPIECZNA WYSOKOSC ~
    Q260=+100  ;BEZPIECZNA WYSOKOSC ~
    Q305=+0    ;NR W TABELI ~
    Q405=+0    ;PUNKT ODNIESIENIA ~
    Q303=+1    ;PRZEKAZ DANYCH POM. ~
    Q381=+0    ;PROBKOW. NA OSI TS ~
    Q382=+0    ;1.WSPOL. DLA OSI TS ~
    Q383=+0    ;2.WSPOLRZ.DLA OSI TS ~
    Q384=+0    ;3. WSPOL. DLA OSI TS ~
    Q333=+0    ;PUNKT ODNIESIENIA
18 ;
19 CYCL DEF 247 USTAWIENIE PKT.BAZ ~
    Q339=+1    ;NR PKT BAZOWEGO
20 ;-------------------------------------------
21 STOP
22 ; WPISZ PONIZEJ SZEROKOSC MOSTKA W MIEJSCU SPLASZCZENIA ( W OSI Y ) JAKO~
 WARTOSC Q1, ZAOKRAGLAJAC DO DWOCH MIEJSC PO PRZECINKU
23 ;
24 FN 0: Q1 =+78.56
25 ;
26 STOP
27 * - GLOWICA-20-ALU
28 TOOL CALL 16 Z S15000 F1800
29 TOOL DEF 30
30 M3 M8
31 L  X-47  Y+0 FMAX
32 L  Z+60 FMAX
33 L  Z+30 F1800
34 ;
35 FN 2: Q1 =+Q1 - +1
36 FN 4: Q2 =+Q1 DIV +2
37 FN 2: Q3 =+Q2 - +10
38 ;
39 LBL 1
40 L IZ-1
41 L  Y-Q3 FMAX
42 L  X-20  Y-Q3
43 RND R14
44 L  X-20  Y+Q2
45 RND R14
46 L  X-47
47 LBL 0
48 ;
49 CALL LBL 1 REP61
50 ;
51 L  Z+60 FMAX
52 L  X+47  Y+0 FMAX
53 L  Z+30 F1800
54 ;
55 LBL 2
56 L IZ-1
57 L  Y-Q3 FMAX
58 L  X+20  Y-Q3
59 RND R14
60 L  X+20  Y+Q2
61 RND R14
62 L  X+47
63 LBL 0
64 ;
65 CALL LBL 2 REP61
66 M5 M9
67 ;
68 ;--------------------------------------
69 L  Z+0 R0 FMAX M91
70 L  X+100  Y+535 R0 FMAX
71 L M30
72 * - -----------------------------------
73 END PGM SPLASZCZENIE-WALEK MM 
