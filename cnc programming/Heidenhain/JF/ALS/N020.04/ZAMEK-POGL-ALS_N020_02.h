0  BEGIN PGM ZAMEK-POGL-ALS_N020_02 MM 
1  BLK FORM 0.1 Z  X-30.1  Y-0.2  Z-13.8
2  BLK FORM 0.2  X+30.1  Y+26.2  Z+0
3  ;---------------------------------------------
4  CYCL DEF 247 USTAWIENIE PKT.BAZ ~
    Q339=+1    ;NR PKT BAZOWEGO
5  ;
6  L  X+0  Y+0 R0 FMAX
7  TOOL CALL 30 Z
8  TOOL DEF 11
9  ;
10 TCH PROBE 409 PKT BAZ.SR.MOSTKA ~
    Q321=+0    ;SRODEK W 1-SZEJ OSI ~
    Q322=+14   ;SRODEK W 2-SZEJ OSI ~
    Q311=+60.6 ;SZEROKOSC MOSTKA ~
    Q272=+1    ;OS POMIAROWA ~
    Q261=-3    ;WYSOKOSC POMIARU ~
    Q320=+10   ;BEZPIECZNA WYSOKOSC ~
    Q260=+50   ;BEZPIECZNA WYSOKOSC ~
    Q305=+1    ;NR W TABELI ~
    Q405=+0    ;PUNKT ODNIESIENIA ~
    Q303=+1    ;PRZEKAZ DANYCH POM. ~
    Q381=+0    ;PROBKOW. NA OSI TS ~
    Q382=+0    ;1.WSPOL. DLA OSI TS ~
    Q383=+0    ;2.WSPOLRZ.DLA OSI TS ~
    Q384=+0    ;3. WSPOL. DLA OSI TS ~
    Q333=+0    ;PUNKT ODNIESIENIA
11 ;
12 CYCL DEF 247 USTAWIENIE PKT.BAZ ~
    Q339=+1    ;NR PKT BAZOWEGO
13 ;---------------------------------------------
14 ;
15 * - POGL 17  FP-8
16 TOOL CALL 11 Z S1400 F200
17 TOOL DEF 9
18 M8
19 CYCL DEF 208 SPIRALNE FREZ. OTW. ~
    Q200=+1    ;BEZPIECZNA WYSOKOSC ~
    Q201=-7    ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q334=+1    ;GLEBOKOSC DOSUWU ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q335=+17   ;SREDNICA NOMINALNA ~
    Q342=+11   ;WYW.WSTEP. SREDNICA ~
    Q351=+1    ;RODZAJ FREZOWANIA
20 CALL LBL 1
21 M5 M9
22 ;
23 * - FAZY INTERPOLACJA
24 * - UWAGA! FAZOWNIK MUSI MIEC R=2 W TABELI NARZEDZI!
25 TOOL CALL 9 Z S1600 F150
26 TOOL DEF 30
27 M8
28 CYCL DEF 208 SPIRALNE FREZ. OTW. ~
    Q200=+1    ;BEZPIECZNA WYSOKOSC ~
    Q201=-2    ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q334=+3.5  ;GLEBOKOSC DOSUWU ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q335=+17   ;SREDNICA NOMINALNA ~
    Q342=+15   ;WYW.WSTEP. SREDNICA ~
    Q351=+1    ;RODZAJ FREZOWANIA
29 CALL LBL 1
30 M5 M9
31 ;
32 ;---------------------------------------------
33 L  Z+0 R0 FMAX M91
34 L  X+700  Y+535 R0 FMAX M91
35 L M30
36 * - -----------------------------------------
37 * - LBL1 POGLEBIENIA 17X7
38 LBL 1
39 M3
40 L  X-12.5  Y+11 R0 FMAX M99
41 L  X+12.5  Y+11 R0 FMAX M99
42 LBL 0
43 END PGM ZAMEK-POGL-ALS_N020_02 MM 
