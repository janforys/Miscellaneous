0  BEGIN PGM STR-2 MM 
1  BLK FORM 0.1 Z  X-40  Y+0  Z-32
2  BLK FORM 0.2  X+40  Y+22  Z+1
3  ;---------------------------------------------
4  CYCL DEF 247 USTAWIENIE PKT.BAZ ~
    Q339=+1    ;NR PKT BAZOWEGO
5  CYCL DEF 7.0 PUNKT BAZOWY
6  CYCL DEF 7.1 IY+0
7  ;
8  STOP
9  L  X+0  Y+0 R0 FMAX
10 ;
11 TOOL CALL 30 Z
12 TOOL DEF 24
13 ;
14 TCH PROBE 409 PKT BAZ.SR.MOSTKA ~
    Q321=+0    ;SRODEK W 1-SZEJ OSI ~
    Q322=+11   ;SRODEK W 2-SZEJ OSI ~
    Q311=+80   ;SZEROKOSC MOSTKA ~
    Q272=+1    ;OS POMIAROWA ~
    Q261=-4    ;WYSOKOSC POMIARU ~
    Q320=+20   ;BEZPIECZNA WYSOKOSC ~
    Q260=+30   ;BEZPIECZNA WYSOKOSC ~
    Q305=+1    ;NR W TABELI ~
    Q405=+0    ;PUNKT ODNIESIENIA ~
    Q303=+1    ;PRZEKAZ DANYCH POM. ~
    Q381=+0    ;PROBKOW. NA OSI TS ~
    Q382=+0    ;1.WSPOL. DLA OSI TS ~
    Q383=+0    ;2.WSPOLRZ.DLA OSI TS ~
    Q384=+0    ;3. WSPOL. DLA OSI TS ~
    Q333=+0    ;PUNKT ODNIESIENIA
15 ;
16 CYCL DEF 247 USTAWIENIE PKT.BAZ ~
    Q339=+1    ;NR PKT BAZOWEGO
17 ;---------------------------------------------
18 ;
19 * - PLANOWANIE
20 TOOL CALL 24 Z S2222 F160
21 STOP
22 TOOL DEF 18
23 M3 M8
24 CYCL DEF 232 FREZOW.PLANOWE ~
    Q389=+2    ;STRATEGIA ~
    Q225=-40   ;PKT.STARTU 1SZEJ OSI ~
    Q226=+0    ;PKT.STARTU 2GIEJ OSI ~
    Q227=+0.15 ;PKT.STARTU 3CIEJ OSI ~
    Q386=+0    ;PUNKT KONCOWY 3. OSI ~
    Q218=+80   ;DLUG. 1-SZEJ STRONY ~
    Q219=+22   ;DLUG. 2-GIEJ STRONY ~
    Q202=+0.2  ;MAX. GLEB. DOSUWU ~
    Q369=+0    ;NADDATEK NA DNIE ~
    Q370=+0.9  ;MAX. NAKLADANIE ~
    Q207= AUTO ;POSUW FREZOWANIA ~
    Q385= AUTO ;POSUW OBR.WYKAN. ~
    Q253= MAX ;PREDK. POS. ZAGLEB. ~
    Q200=+5    ;BEZPIECZNA WYSOKOSC ~
    Q357=+5    ;ODST. BEZP. Z BOKU ~
    Q204=+50   ;2-GA BEZPIECZNA WYS.
25 CYCL CALL
26 M5 M9
27 ;
28 * - FI11
29 TOOL CALL 18 Z S2222 F160
30 STOP
31 TOOL DEF 30
32 M3 M8
33 CYCL DEF 208 SPIRALNE FREZ. OTW. ~
    Q200=+0.5  ;BEZPIECZNA WYSOKOSC ~
    Q201=-9    ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q334=+0.5  ;GLEBOKOSC DOSUWU ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q335=+11   ;SREDNICA NOMINALNA ~
    Q342=+0    ;WYW.WSTEP. SREDNICA ~
    Q351=+1    ;RODZAJ FREZOWANIA
34 CALL LBL 1
35 M5 M9
36 ;
37 * - FAZA
38 TOOL CALL 9 Z S2000 F300 DR-3
39 STOP
40 TOOL DEF 26
41 M3 M8
42 CYCL DEF 256 CZOP PROSTOKATNY ~
    Q218=+80   ;DLUG. 1-SZEJ STRONY ~
    Q424=+80.1 ;WYMIAR POLWYROBU 1 ~
    Q219=+22   ;DLUG. 2-GIEJ STRONY ~
    Q425=+22.1 ;WYMIAR POLWYROBU 2 ~
    Q220=+0    ;PROMIEN NAROZA ~
    Q368=+0    ;NADDATEK NA STRONE ~
    Q224=+0    ;KAT OBROTU ~
    Q367=+0    ;POLOZENIE CZOPU ~
    Q207= AUTO ;POSUW FREZOWANIA ~
    Q351=+1    ;RODZAJ FREZOWANIA ~
    Q201=-1.3  ;GLEBOKOSC ~
    Q202=+2.3  ;GLEBOKOSC DOSUWU ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q200=+1    ;BEZPIECZNA WYSOKOSC ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q370=+1    ;ZACHODZENIE TOROW ~
    Q437=+1    ;POZYCJA NAJAZDU
43 CALL LBL 3
44 M5 M9
45 ;
46 * - FAZO
47 TOOL CALL 26 Z S650 F30 DL+0.1
48 TOOL DEF 30
49 M3 M8
50 CYCL DEF 200 WIERCENIE ~
    Q200=+0    ;BEZPIECZNA WYSOKOSC ~
    Q201=-1    ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q202=+1    ;GLEBOKOSC DOSUWU ~
    Q210=+0    ;PRZER. CZAS.NA GORZE ~
    Q203=-4.5  ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q211=+0    ;PRZERWA CZAS. DNIE ~
    Q395=+0    ;REFERENCJA GLEB.
51 CALL LBL 1
52 M5 M9
53 ;
54 ;---------------------------------------------
55 L  Z-5 R0 FMAX M91
56 L  X+260  Y+535 R0 FMAX M91
57 L M30
58 * - -----------------------------------------
59 * - LBL1 11/6.4
60 LBL 1
61 M3
62 L  X-32.5  Y+6 R0 FMAX M99
63 L  X+32.5  Y+6 R0 FMAX M99
64 LBL 0
65 * - LBL3 FAZKA
66 LBL 3
67 M3
68 L  X+0  Y+11 R0 FMAX M99
69 LBL 0
70 END PGM STR-2 MM 
