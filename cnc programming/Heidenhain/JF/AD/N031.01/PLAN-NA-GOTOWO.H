0  BEGIN PGM PLAN-NA-GOTOWO MM 
1  BLK FORM 0.1 Z  X-45  Y-16  Z-32
2  BLK FORM 0.2  X+45  Y+16  Z+9
3  ;---------------------------------------------
4  CYCL DEF 247 USTAWIENIE PKT.BAZ ~
    Q339=+1    ;NR PKT BAZOWEGO
5  ;---------------------------------------------
6  ;
7  * - PLAN ZGR
8  STOP
9  TOOL CALL 14 Z S1600 F800
10 TOOL DEF 24
11 M8 M3
12 CYCL DEF 232 FREZOW.PLANOWE ~
    Q389=+1    ;STRATEGIA ~
    Q225=-53   ;PKT.STARTU 1SZEJ OSI ~
    Q226=-20   ;PKT.STARTU 2GIEJ OSI ~
    Q227=+9    ;PKT.STARTU 3CIEJ OSI ~
    Q386=+0.15 ;PUNKT KONCOWY 3. OSI ~
    Q218=+106  ;DLUG. 1-SZEJ STRONY ~
    Q219=+40   ;DLUG. 2-GIEJ STRONY ~
    Q202=+0.8  ;MAX. GLEB. DOSUWU ~
    Q369=+0    ;NADDATEK NA DNIE ~
    Q370=+1    ;MAX. NAKLADANIE ~
    Q207= AUTO ;POSUW FREZOWANIA ~
    Q385= AUTO ;POSUW OBR.WYKAN. ~
    Q253= MAX ;PREDK. POS. ZAGLEB. ~
    Q200=+5    ;BEZPIECZNA WYSOKOSC ~
    Q357=+5    ;ODST. BEZP. Z BOKU ~
    Q204=+50   ;2-GA BEZPIECZNA WYS.
13 CYCL CALL
14 M5 M9
15 ;
16 STOP
17 * -        STOP! ZDEJMIJ RESZTKI
18 ;
19 * - PLAN WYK
20 TOOL CALL 24 Z S3000 F600
21 TOOL DEF 30
22 M8 M3
23 CYCL DEF 232 FREZOW.PLANOWE ~
    Q389=+2    ;STRATEGIA ~
    Q225=-50   ;PKT.STARTU 1SZEJ OSI ~
    Q226=-12   ;PKT.STARTU 2GIEJ OSI ~
    Q227=+0.15 ;PKT.STARTU 3CIEJ OSI ~
    Q386=+0    ;PUNKT KONCOWY 3. OSI ~
    Q218=+100  ;DLUG. 1-SZEJ STRONY ~
    Q219=+24   ;DLUG. 2-GIEJ STRONY ~
    Q202=+0.3  ;MAX. GLEB. DOSUWU ~
    Q369=+0    ;NADDATEK NA DNIE ~
    Q370=+1    ;MAX. NAKLADANIE ~
    Q207= AUTO ;POSUW FREZOWANIA ~
    Q385= AUTO ;POSUW OBR.WYKAN. ~
    Q253= MAX ;PREDK. POS. ZAGLEB. ~
    Q200=+5    ;BEZPIECZNA WYSOKOSC ~
    Q357=+5    ;ODST. BEZP. Z BOKU ~
    Q204=+50   ;2-GA BEZPIECZNA WYS.
24 CYCL CALL
25 M5 M9
26 ;
27 ;---------------------------------------------
28 CYCL DEF 247 USTAWIENIE PKT.BAZ ~
    Q339=+1    ;NR PKT BAZOWEGO
29 ;
/ 30 STOP
31 L  X+0  Y+0 R0 FMAX
32 ;
33 TOOL CALL 30 Z
34 TOOL DEF 9
35 ;
36 TCH PROBE 411 PKT.BAZ.PROST.ZEWN. ~
    Q321=+0    ;SRODEK W 1-SZEJ OSI ~
    Q322=+0    ;SRODEK W 2-SZEJ OSI ~
    Q323=+80   ;DLUG. 1-SZEJ STRONY ~
    Q324=+22   ;DLUG. 2-GIEJ STRONY ~
    Q261=-4    ;WYSOKOSC POMIARU ~
    Q320=+20   ;BEZPIECZNA WYSOKOSC ~
    Q260=+30   ;BEZPIECZNA WYSOKOSC ~
    Q301=+1    ;ODJAZD NA BEZP.WYS. ~
    Q305=+1    ;NR W TABELI ~
    Q331=+0    ;PUNKT ODNIESIENIA ~
    Q332=+0    ;PUNKT ODNIESIENIA ~
    Q303=+1    ;PRZEKAZ DANYCH POM. ~
    Q381=+0    ;PROBKOW. NA OSI TS ~
    Q382=+0    ;1.WSPOL. DLA OSI TS ~
    Q383=+0    ;2.WSPOLRZ.DLA OSI TS ~
    Q384=+0    ;3. WSPOL. DLA OSI TS ~
    Q333=+0    ;PUNKT ODNIESIENIA
37 ;
38 CYCL DEF 247 USTAWIENIE PKT.BAZ ~
    Q339=+1    ;NR PKT BAZOWEGO
39 ;
40 ;---------------------------------------------
41 ;
42 * - GRATOWANIE OBRYS
43 TOOL CALL 9 Z S3000 F200 DR-3
/ 44 STOP
45 TOOL DEF 18
46 M8 M3
47 CYCL DEF 256 CZOP PROSTOKATNY ~
    Q218=+80   ;DLUG. 1-SZEJ STRONY ~
    Q424=+80.1 ;WYMIAR POLWYROBU 1 ~
    Q219=+22   ;DLUG. 2-GIEJ STRONY ~
    Q425=+22.1 ;WYMIAR POLWYROBU 2 ~
    Q220=+0    ;PROMIEN NAROZA ~
    Q368=+0    ;NADDATEK NA STRONE ~
    Q224=+0    ;KAT OBROTU ~
    Q367=+0    ;POLOZENIE CZOPU ~
    Q207= AUTO ;POSUW FREZOWANIA ~
    Q351=+1    ;RODZAJ FREZOWANIA ~
    Q201=-1    ;GLEBOKOSC ~
    Q202=+2    ;GLEBOKOSC DOSUWU ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q200=+2    ;BEZPIECZNA WYSOKOSC ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q370=+1    ;ZACHODZENIE TOROW ~
    Q437=+1    ;POZYCJA NAJAZDU
48 CALL LBL 1
49 M5 M9
50 ;
51 * - FP 8 ZGR POGLEBIENIA
52 TOOL CALL 18 Z S1800 F200
/ 53 STOP
54 TOOL DEF 26
55 M8 M3
56 CYCL DEF 208 SPIRALNE FREZ. OTW. ~
    Q200=+0.5  ;BEZPIECZNA WYSOKOSC ~
    Q201=-9    ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q334=+0.4  ;GLEBOKOSC DOSUWU ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q335=+11.05 ;SREDNICA NOMINALNA ~
    Q342=+0    ;WYW.WSTEP. SREDNICA ~
    Q351=+1    ;RODZAJ FREZOWANIA
57 CALL LBL 2
58 M5 M9
59 ;
60 * - FAZY POGLEBIENIA
61 TOOL CALL 26 Z S600 F40
/ 62 STOP
63 TOOL DEF 14
64 M8 M3
65 CYCL DEF 200 WIERCENIE ~
    Q200=+0    ;BEZPIECZNA WYSOKOSC ~
    Q201=-2.6  ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q202=+2.6  ;GLEBOKOSC DOSUWU ~
    Q210=+0    ;PRZER. CZAS.NA GORZE ~
    Q203=-3    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q211=+0.3  ;PRZERWA CZAS. DNIE ~
    Q395=+0    ;REFERENCJA GLEB.
66 CALL LBL 2
67 M5 M9
68 ;
69 ;---------------------------------------------
70 L  Z-5 R0 FMAX M91
71 L  X+260  Y+535 R0 FMAX M91
72 L M30
73 * - -----------------------------------------
74 * - LBL1 SRODEK
75 LBL 1
76 M3
77 L  X+0  Y+0 R0 FMAX M99
78 LBL 0
79 * - LBL2 POGLEBIENIA 11
80 LBL 2
81 M3
82 L  X-32.5  Y-5 R0 FMAX M99
83 L  X+32.5  Y-5 R0 FMAX M99
84 LBL 0
85 ;
86 END PGM PLAN-NA-GOTOWO MM 
