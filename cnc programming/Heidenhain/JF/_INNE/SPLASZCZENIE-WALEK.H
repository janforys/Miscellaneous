0  BEGIN PGM SPLASZCZENIE-WALEK MM 
1  BLK FORM 0.1 Z  X-49.5  Y-100  Z-36
2  BLK FORM 0.2  X+49.5  Y+100  Z+36
3  ;-------------------------------------------
4  ;
5  ;-------------------------------------------
6  ; USTAW BAZE W OSI Y DOKLADNIE PO SRODKU SPLASZCZENIA ( SRODEK MOSTKA )
7  ; USTAW "Z" W OSI DETALU
8  ; USTAW BAZE W OSI X NA SRODKU DETALU
9  ; WYSUNIECIE NARZEDZIA - 82mm
10 ;-------------------------------------------
/ 11 STOP
12 CYCL DEF 247 USTAWIENIE PKT.BAZ ~
    Q339=+1    ;NR PKT BAZOWEGO
13 L  X+0  Y+0 R0 FMAX
14 * - SONDA
/ 15 STOP
16 TOOL CALL 30 Z S50
17 TOOL DEF 19
18 TCH PROBE 409 PKT BAZ.SR.MOSTKA ~
    Q321=+0    ;SRODEK W 1-SZEJ OSI ~
    Q322=-52   ;SRODEK W 2-SZEJ OSI ~
    Q311=+95   ;SZEROKOSC MOSTKA ~
    Q272=+1    ;OS POMIAROWA ~
    Q261=+15   ;WYSOKOSC POMIARU ~
    Q320=+15   ;BEZPIECZNA WYSOKOSC ~
    Q260=+75   ;BEZPIECZNA WYSOKOSC ~
    Q305=+1    ;NR W TABELI ~
    Q405=+0    ;PUNKT ODNIESIENIA ~
    Q303=+1    ;PRZEKAZ DANYCH POM. ~
    Q381=+0    ;PROBKOW. NA OSI TS ~
    Q382=+0    ;1.WSPOL. DLA OSI TS ~
    Q383=+0    ;2.WSPOLRZ.DLA OSI TS ~
    Q384=+0    ;3. WSPOL. DLA OSI TS ~
    Q333=+0    ;PUNKT ODNIESIENIA
19 ;
20 CYCL DEF 247 USTAWIENIE PKT.BAZ ~
    Q339=+1    ;NR PKT BAZOWEGO
21 ;-------------------------------------------
/ 22 STOP
23 ; WPISZ PONIZEJ SZEROKOSC MOSTKA W MIEJSCU SPLASZCZENIA ( W OSI Y ) JAKO~
 WARTOSC Q1, ZAOKRAGLAJAC DO DWOCH MIEJSC PO PRZECINKU
24 ;
25 FN 0: Q1 =+94.11
26 ;
/ 27 STOP
28 * - GLOWICA-20-ALU
29 TOOL CALL 19 Z S15000 F1800
30 TOOL DEF 30
31 M3 M8
32 L  X-50  Y+0 FMAX
33 L  Z+60 FMAX
34 L  Z+30 F1800
35 ;
36 FN 2: Q1 =+Q1 - +1
37 FN 4: Q2 =+Q1 DIV +2
38 FN 2: Q3 =+Q2 - +10
39 ;
40 LBL 1
41 L IZ-1
42 L  Y-Q3 FMAX
43 L  X-30  Y-Q3
44 RND R4
45 L  X-30  Y+Q3
46 RND R4
47 L  X-50
48 LBL 0
49 ;
50 CALL LBL 1 REP61
51 ;
52 L  Z+60 FMAX
53 L  X+50  Y+0 FMAX
54 L  Z+30 F1800
55 ;
56 LBL 2
57 L IZ-1
58 L  Y-Q3 FMAX
59 L  X+30  Y-Q3
60 RND R4
61 L  X+30  Y+Q3
62 RND R4
63 L  X+50
64 LBL 0
65 ;
66 CALL LBL 2 REP61
67 M5 M9
68 ;
69 ;--------------------------------------
70 L  Z+0 R0 FMAX M91
71 L  X+260  Y+535 R0 FMAX M91
72 L M30
73 * - -----------------------------------
74 END PGM SPLASZCZENIE-WALEK MM 
