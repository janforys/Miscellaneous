0  BEGIN PGM RPI-BASE MM 
1  BLK FORM 0.1 Z  X-32.5  Y-15  Z-18
2  BLK FORM 0.2  X+32.5  Y+15  Z+0.1
3  ;---------------------------------------------
4  CYCL DEF 247 USTAWIENIE PKT.BAZ ~
    Q339=+1    ;NR PKT BAZOWEGO
5  ;---------------------------------------------
6  STOP
7  ;
8  * - FP-8 -PLAN
9  TOOL CALL 28 Z S9987 F700
10 STOP
11 M8
12 CYCL DEF 232 FREZOW.PLANOWE ~
    Q389=+2    ;STRATEGIA ~
    Q225=-35   ;PKT.STARTU 1SZEJ OSI ~
    Q226=-16   ;PKT.STARTU 2GIEJ OSI ~
    Q227=+1    ;PKT.STARTU 3CIEJ OSI ~
    Q386=+0    ;PUNKT KONCOWY 3. OSI ~
    Q218=+75   ;DLUG. 1-SZEJ STRONY ~
    Q219=+35   ;DLUG. 2-GIEJ STRONY ~
    Q202=+1    ;MAX. GLEB. DOSUWU ~
    Q369=+0    ;NADDATEK NA DNIE ~
    Q370=+0.9  ;MAX. NAKLADANIE ~
    Q207= AUTO ;POSUW FREZOWANIA ~
    Q385= AUTO ;POSUW OBR.WYKAN. ~
    Q253= MAX ;PREDK. POS. ZAGLEB. ~
    Q200=+5    ;BEZPIECZNA WYSOKOSC ~
    Q357=+5    ;ODST. BEZP. Z BOKU ~
    Q204=+50   ;2-GA BEZPIECZNA WYS.
13 CALL LBL 2
14 M5 M9
15 ;
16 * - FP-12 -GAB
17 TOOL CALL 28 Z S9987 F650
18 STOP
19 M8
20 CYCL DEF 256 CZOP PROSTOKATNY ~
    Q218=+65   ;DLUG. 1-SZEJ STRONY ~
    Q424=+70   ;WYMIAR POLWYROBU 1 ~
    Q219=+30   ;DLUG. 2-GIEJ STRONY ~
    Q425=+37   ;WYMIAR POLWYROBU 2 ~
    Q220=+3.5  ;PROMIEN NAROZA ~
    Q368=+0    ;NADDATEK NA STRONE ~
    Q224=+0    ;KAT OBROTU ~
    Q367=+0    ;POLOZENIE CZOPU ~
    Q207= AUTO ;POSUW FREZOWANIA ~
    Q351=+1    ;RODZAJ FREZOWANIA ~
    Q201=-14.4 ;GLEBOKOSC ~
    Q202=+2    ;GLEBOKOSC DOSUWU ~
    Q206= MAX ;WARTOSC POSUWU WGL. ~
    Q200=+5    ;BEZPIECZNA WYSOKOSC ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q370=+1    ;ZACHODZENIE TOROW ~
    Q437=+0    ;POZYCJA NAJAZDU
21 CALL LBL 2
22 M5 M9
23 ;
24 * - FP-12 -GAB WYK
25 TOOL CALL 28 Z S9987 F700 DR-0.02
26 STOP
27 M8
28 CYCL DEF 256 CZOP PROSTOKATNY ~
    Q218=+65   ;DLUG. 1-SZEJ STRONY ~
    Q424=+70   ;WYMIAR POLWYROBU 1 ~
    Q219=+30   ;DLUG. 2-GIEJ STRONY ~
    Q425=+37   ;WYMIAR POLWYROBU 2 ~
    Q220=+3.5  ;PROMIEN NAROZA ~
    Q368=+0    ;NADDATEK NA STRONE ~
    Q224=+0    ;KAT OBROTU ~
    Q367=+0    ;POLOZENIE CZOPU ~
    Q207= AUTO ;POSUW FREZOWANIA ~
    Q351=+1    ;RODZAJ FREZOWANIA ~
    Q201=-14.4 ;GLEBOKOSC ~
    Q202=+14.4 ;GLEBOKOSC DOSUWU ~
    Q206= MAX ;WARTOSC POSUWU WGL. ~
    Q200=+5    ;BEZPIECZNA WYSOKOSC ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q370=+1    ;ZACHODZENIE TOROW ~
    Q437=+0    ;POZYCJA NAJAZDU
29 CALL LBL 2
30 M5 M9
31 ;
32 * - NAW
33 TOOL CALL 9 Z S1111 F111
34 STOP
35 M8
36 CYCL DEF 200 WIERCENIE ~
    Q200=+2    ;BEZPIECZNA WYSOKOSC ~
    Q201=-1.6  ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q202=+2    ;GLEBOKOSC DOSUWU ~
    Q210=+0    ;PRZER. CZAS.NA GORZE ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q211=+0    ;PRZERWA CZAS. DNIE ~
    Q395=+0    ;REFERENCJA GLEB.
37 CALL LBL 1
38 M5 M9
39 ;
40 * - W2.5
41 TOOL CALL 3 Z S2800 F100
42 STOP
43 TOOL DEF 23
44 M8
45 CYCL DEF 200 WIERCENIE ~
    Q200=+2    ;BEZPIECZNA WYSOKOSC ~
    Q201=-23   ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q202=+2    ;GLEBOKOSC DOSUWU ~
    Q210=+0    ;PRZER. CZAS.NA GORZE ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q211=+0    ;PRZERWA CZAS. DNIE ~
    Q395=+0    ;REFERENCJA GLEB.
46 CALL LBL 1
47 M5 M9
48 ;
49 * - M3
50 TOOL CALL 5 Z S100
51 STOP
52 TOOL DEF 23
53 M8
54 CYCL DEF 207 GWINTOWANIE GS  ~
    Q200=+3    ;BEZPIECZNA WYSOKOSC ~
    Q201=-6    ;GLEBOKOSC GWINTU ~
    Q239=+0.5  ;SKOK GWINTU ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS.
55 CALL LBL 1
56 M5 M9
57 ;
58 * - FP-12 -WYSPY
59 TOOL CALL 28 Z S9987 F400 DL+0.2
60 STOP
61 M8
62 CYCL DEF 257 CZOP OKRAGLY ~
    Q223=+6    ;SRED.WYBR.OBR.NA GOT ~
    Q222=+7    ;SREDNICA WST.OBR.WYB ~
    Q368=+0    ;NADDATEK NA STRONE ~
    Q207= AUTO ;POSUW FREZOWANIA ~
    Q351=+1    ;RODZAJ FREZOWANIA ~
    Q201=-4    ;GLEBOKOSC ~
    Q202=+1    ;GLEBOKOSC DOSUWU ~
    Q206=+250  ;WARTOSC POSUWU WGL. ~
    Q200=+0.5  ;BEZPIECZNA WYSOKOSC ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q370=+1    ;ZACHODZENIE TOROW ~
    Q376=-1    ;KAT POCZATKOWY
63 CALL LBL 1
64 M5 M9
65 ;
66 * - FP-12 -WYBRANIE
67 TOOL CALL 28 Z S9987 F600 DL+0.2
68 STOP
69 M8
70 CYCL DEF 251 KIESZEN PROSTOKATNA ~
    Q215=+0    ;RODZAJ OBROBKI ~
    Q218=+45   ;DLUG. 1-SZEJ STRONY ~
    Q219=+45   ;DLUG. 2-GIEJ STRONY ~
    Q220=+0    ;PROMIEN NAROZA ~
    Q368=+0    ;NADDATEK NA STRONE ~
    Q224=+0    ;KAT OBROTU ~
    Q367=+0    ;POLOZENIE KIESZENI ~
    Q207= AUTO ;POSUW FREZOWANIA ~
    Q351=+1    ;RODZAJ FREZOWANIA ~
    Q201=-4    ;GLEBOKOSC ~
    Q202=+1    ;GLEBOKOSC DOSUWU ~
    Q369=+0    ;NADDATEK NA DNIE ~
    Q206=+250  ;WARTOSC POSUWU WGL. ~
    Q338=+0    ;DOSUW - OBR.WYKONCZ. ~
    Q200=+0.5  ;BEZPIECZNA WYSOKOSC ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q370=+1    ;ZACHODZENIE TOROW ~
    Q366=+1    ;ZAGLEBIANIE ~
    Q385= AUTO ;POSUW OBR.WYKAN.
71 CALL LBL 2
72 M5 M9
73 ;
74 * - FP-12 -WYSPY
75 TOOL CALL 28 Z S9987 F400 DL-0.05
76 STOP
77 M8
78 CYCL DEF 257 CZOP OKRAGLY ~
    Q223=+6    ;SRED.WYBR.OBR.NA GOT ~
    Q222=+7    ;SREDNICA WST.OBR.WYB ~
    Q368=+0    ;NADDATEK NA STRONE ~
    Q207= AUTO ;POSUW FREZOWANIA ~
    Q351=+1    ;RODZAJ FREZOWANIA ~
    Q201=-4    ;GLEBOKOSC ~
    Q202=+4    ;GLEBOKOSC DOSUWU ~
    Q206=+250  ;WARTOSC POSUWU WGL. ~
    Q200=+0.5  ;BEZPIECZNA WYSOKOSC ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q370=+1    ;ZACHODZENIE TOROW ~
    Q376=-1    ;KAT POCZATKOWY
79 CALL LBL 1
80 M5 M9
81 ;
82 * - FP-12 -WYBRANIE
83 TOOL CALL 28 Z S9987 F600 DL-0.05
84 STOP
85 M8
86 CYCL DEF 251 KIESZEN PROSTOKATNA ~
    Q215=+0    ;RODZAJ OBROBKI ~
    Q218=+45   ;DLUG. 1-SZEJ STRONY ~
    Q219=+45   ;DLUG. 2-GIEJ STRONY ~
    Q220=+0    ;PROMIEN NAROZA ~
    Q368=+0    ;NADDATEK NA STRONE ~
    Q224=+0    ;KAT OBROTU ~
    Q367=+0    ;POLOZENIE KIESZENI ~
    Q207= AUTO ;POSUW FREZOWANIA ~
    Q351=+1    ;RODZAJ FREZOWANIA ~
    Q201=-4    ;GLEBOKOSC ~
    Q202=+4    ;GLEBOKOSC DOSUWU ~
    Q369=+0    ;NADDATEK NA DNIE ~
    Q206=+250  ;WARTOSC POSUWU WGL. ~
    Q338=+0    ;DOSUW - OBR.WYKONCZ. ~
    Q200=+0.5  ;BEZPIECZNA WYSOKOSC ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q370=+1    ;ZACHODZENIE TOROW ~
    Q366=+1    ;ZAGLEBIANIE ~
    Q385= AUTO ;POSUW OBR.WYKAN.
87 CALL LBL 2
88 M5 M9
89 ;
90 * - FAZY
91 TOOL CALL 9 Z S5000 F400 DL-0.5 DR-3
92 STOP
93 M8 M3
94 L  X-29  Y+11.5 R0 FMAX
95 CYCL DEF 257 CZOP OKRAGLY ~
    Q223=+6    ;SRED.WYBR.OBR.NA GOT ~
    Q222=+6.1  ;SREDNICA WST.OBR.WYB ~
    Q368=+0    ;NADDATEK NA STRONE ~
    Q207= AUTO ;POSUW FREZOWANIA ~
    Q351=+1    ;RODZAJ FREZOWANIA ~
    Q201=-0.4  ;GLEBOKOSC ~
    Q202=+2    ;GLEBOKOSC DOSUWU ~
    Q206= MAX ;WARTOSC POSUWU WGL. ~
    Q200=+0    ;BEZPIECZNA WYSOKOSC ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q370=+1    ;ZACHODZENIE TOROW ~
    Q376=-1    ;KAT POCZATKOWY
96 CYCL CALL
97 L  X+29  Y+11.5 R0 FMAX
98 CYCL CALL
99 L  X-29  Y-11.5 R0 FMAX
100 CYCL CALL
101 L  X+29  Y-11.5 R0 FMAX
102 CYCL CALL
103 M5 M9
104 ;
105 * - KULA 1 DLA WIERZCHOLKA
106 TOOL CALL 2 Z S9987 F50 DL-0.05
107 STOP
108 M3 M8
109 L  X-10.5  Y-2 R0 FMAX
110 CYCL DEF 225 GRAWEROWANIE ~
    QS500="RPi-Zero" ;TEKST GRAWER. ~
    Q513=+4.3  ;WYSOK.ZNAKU ~
    Q514=+0.7  ;WSPOL.ODSTEPU ~
    Q515=+0    ;FONT ~
    Q516=+0    ;UKLAD TEKSTU ~
    Q374=+0    ;KAT OBROTU ~
    Q517=+50   ;PROMIEN OKREGU ~
    Q207= AUTO ;POSUW FREZOWANIA ~
    Q201=-0.06 ;GLEBOKOSC ~
    Q206=+20   ;WARTOSC POSUWU WGL. ~
    Q200=+0.5  ;BEZPIECZNA WYSOKOSC ~
    Q203=-4    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS.
111 CYCL CALL
112 M5 M9
113 ;
114 ;---------------------------------------------
115 L  Z-5 R0 FMAX M91
116 L  X+260  Y+535 R0 FMAX M91
117 L M30
118 * - -----------------------------------------
119 * - LBL1 GW/OTW
120 LBL 1
121 M3
122 L  X-29  Y+11.5 R0 FMAX M99
123 L  X+29  Y+11.5 R0 FMAX M99
124 L  X-29  Y-11.5 R0 FMAX M99
125 L  X+29  Y-11.5 R0 FMAX M99
126 LBL 0
127 * - LBL2 SRODEK
128 LBL 2
129 M3
130 L  X+0  Y+0 R0 FMAX M99
131 LBL 0
132 END PGM RPI-BASE MM 
