0  BEGIN PGM KR_N10103-1STR MM 
1  BLK FORM 0.1 Z  X-50  Y-50  Z-47.2
2  BLK FORM 0.2  X+50  Y+50  Z+0
3  ;---------------------------------------------
4  CYCL DEF 247 USTAWIENIE PKT.BAZ ~
    Q339=+1    ;NR PKT BAZOWEGO
5  ;
/ 6  STOP
7  L  X+0  Y+0 R0 FMAX
8  TOOL CALL 30 Z
9  TOOL DEF 9
10 ;
11 TCH PROBE 412 PKT.BAZ.OKRAG WEWN. ~
    Q321=+0    ;SRODEK W 1-SZEJ OSI ~
    Q322=+0    ;SRODEK W 2-SZEJ OSI ~
    Q262=+68   ;SREDNICA NOMINALNA ~
    Q325=+0    ;KAT POCZATKOWY ~
    Q247=+90   ;KATOWY PRZYROST-KROK ~
    Q261=-2    ;WYSOKOSC POMIARU ~
    Q320=+10   ;BEZPIECZNA WYSOKOSC ~
    Q260=+10   ;BEZPIECZNA WYSOKOSC ~
    Q301=+0    ;ODJAZD NA BEZP.WYS. ~
    Q305=+1    ;NR W TABELI ~
    Q331=+0    ;PUNKT ODNIESIENIA ~
    Q332=+0    ;PUNKT ODNIESIENIA ~
    Q303=+1    ;PRZEKAZ DANYCH POM. ~
    Q381=+0    ;PROBKOW. NA OSI TS ~
    Q382=+0    ;1.WSPOL. DLA OSI TS ~
    Q383=+0    ;2.WSPOLRZ.DLA OSI TS ~
    Q384=+0    ;3. WSPOL. DLA OSI TS ~
    Q333=+0    ;PUNKT ODNIESIENIA ~
    Q423=+4    ;LICZBA PKT POMIAR. ~
    Q365=+1    ;RODZAJ PRZEMIESZCZ.
12 ;
13 CYCL DEF 247 USTAWIENIE PKT.BAZ ~
    Q339=+1    ;NR PKT BAZOWEGO
14 ;---------------------------------------------
15 ;
/ 16 STOP
17 * - NAW 8
18 TOOL CALL 9 Z S1200 F120
19 TOOL DEF 17
20 M8
21 CYCL DEF 200 WIERCENIE ~
    Q200=+2    ;BEZPIECZNA WYSOKOSC ~
    Q201=-3.1  ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q202=+3.1  ;GLEBOKOSC DOSUWU ~
    Q210=+0    ;PRZER. CZAS.NA GORZE ~
    Q203=-9.5  ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q211=+0    ;PRZERWA CZAS. DNIE ~
    Q395=+0    ;REFERENCJA GLEB.
22 CALL LBL 1
23 CALL LBL 2
24 CYCL DEF 200 WIERCENIE ~
    Q200=+2    ;BEZPIECZNA WYSOKOSC ~
    Q201=-3    ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q202=+3    ;GLEBOKOSC DOSUWU ~
    Q210=+0    ;PRZER. CZAS.NA GORZE ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q211=+0    ;PRZERWA CZAS. DNIE ~
    Q395=+0    ;REFERENCJA GLEB.
25 CALL LBL 3
26 M5 M9
27 ;
28 * - W 5.8
29 TOOL CALL 17 Z S1300 F100
/ 30 STOP
31 TOOL DEF 21
32 M8
33 CYCL DEF 200 WIERCENIE ~
    Q200=+2    ;BEZPIECZNA WYSOKOSC ~
    Q201=-9    ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q202=+3    ;GLEBOKOSC DOSUWU ~
    Q210=+0    ;PRZER. CZAS.NA GORZE ~
    Q203=-9.5  ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q211=+0    ;PRZERWA CZAS. DNIE ~
    Q395=+0    ;REFERENCJA GLEB.
34 CALL LBL 1
35 M5 M9
36 ;
37 * - W 6.8
38 TOOL CALL 21 Z S1300 F100
/ 39 STOP
40 TOOL DEF 25
41 M8
42 CYCL DEF 200 WIERCENIE ~
    Q200=+2    ;BEZPIECZNA WYSOKOSC ~
    Q201=-21   ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q202=+3    ;GLEBOKOSC DOSUWU ~
    Q210=+0    ;PRZER. CZAS.NA GORZE ~
    Q203=-9.5  ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q211=+0    ;PRZERWA CZAS. DNIE ~
    Q395=+0    ;REFERENCJA GLEB.
43 CALL LBL 2
44 M5 M9
45 ;
46 * - W 6.6   UWAGA NA PODKLADY
47 TOOL CALL 25 Z S1200 F100
/ 48 STOP
49 TOOL DEF 20
50 M8
51 CYCL DEF 200 WIERCENIE ~
    Q200=+2    ;BEZPIECZNA WYSOKOSC ~
    Q201=-34   ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q202=+3    ;GLEBOKOSC DOSUWU ~
    Q210=+0    ;PRZER. CZAS.NA GORZE ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q211=+0    ;PRZERWA CZAS. DNIE ~
    Q395=+0    ;REFERENCJA GLEB.
52 CALL LBL 3
53 M5 M9
54 ;
55 * - W 8 VHM   WSTEPNIE POD 11.8
56 TOOL CALL 20 Z S3975 F795
/ 57 STOP
58 TOOL DEF 29
59 M8
60 CYCL DEF 200 WIERCENIE ~
    Q200=+2    ;BEZPIECZNA WYSOKOSC ~
    Q201=-25   ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q202=+25   ;GLEBOKOSC DOSUWU ~
    Q210=+0    ;PRZER. CZAS.NA GORZE ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q211=+0    ;PRZERWA CZAS. DNIE ~
    Q395=+0    ;REFERENCJA GLEB.
61 CALL LBL 4
62 M5 M9
63 ;
64 * - FP 8
65 TOOL CALL 29 Z S9987 F600
/ 66 STOP
67 TOOL DEF 26
68 M8
69 ; POGLEBIENIA 
70 CYCL DEF 208 SPIRALNE FREZ. OTW. ~
    Q200=+1    ;BEZPIECZNA WYSOKOSC ~
    Q201=-12   ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q334=+0.8  ;GLEBOKOSC DOSUWU ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q335=+13.05 ;SREDNICA NOMINALNA ~
    Q342=+0    ;WYW.WSTEP. SREDNICA ~
    Q351=+1    ;RODZAJ FREZOWANIA
71 CALL LBL 3
72 ;
73 ; 11.8
74 CYCL DEF 208 SPIRALNE FREZ. OTW. ~
    Q200=+1    ;BEZPIECZNA WYSOKOSC ~
    Q201=-18   ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q334=+0.8  ;GLEBOKOSC DOSUWU ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q335=+11.85 ;SREDNICA NOMINALNA ~
    Q342=+0    ;WYW.WSTEP. SREDNICA ~
    Q351=+1    ;RODZAJ FREZOWANIA
75 CALL LBL 4
76 M5 M9
77 ;
78 * - NAW 16 FAZY
79 TOOL CALL 26 Z S1200 F120
/ 80 STOP
81 TOOL DEF 12
82 M8
83 CYCL DEF 200 WIERCENIE ~
    Q200=+2    ;BEZPIECZNA WYSOKOSC ~
    Q201=-4.1  ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q202=+4.1  ;GLEBOKOSC DOSUWU ~
    Q210=+0    ;PRZER. CZAS.NA GORZE ~
    Q203=-9.5  ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q211=+0    ;PRZERWA CZAS. DNIE ~
    Q395=+0    ;REFERENCJA GLEB.
84 CALL LBL 2
85 CYCL DEF 200 WIERCENIE ~
    Q200=+2    ;BEZPIECZNA WYSOKOSC ~
    Q201=-6.6  ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q202=+6.6  ;GLEBOKOSC DOSUWU ~
    Q210=+0    ;PRZER. CZAS.NA GORZE ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q211=+0    ;PRZERWA CZAS. DNIE ~
    Q395=+0    ;REFERENCJA GLEB.
86 CALL LBL 3
87 CYCL DEF 200 WIERCENIE ~
    Q200=+2    ;BEZPIECZNA WYSOKOSC ~
    Q201=-6.7  ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q202=+6.7  ;GLEBOKOSC DOSUWU ~
    Q210=+0    ;PRZER. CZAS.NA GORZE ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q211=+0    ;PRZERWA CZAS. DNIE ~
    Q395=+0    ;REFERENCJA GLEB.
88 CALL LBL 4
89 M5 M9
90 ;
91 * - R 6H7
92 TOOL CALL 12 Z S150 F30
/ 93 STOP
94 TOOL DEF 27
95 M8
96 CYCL DEF 201 ROZWIERCANIE ~
    Q200=+2    ;BEZPIECZNA WYSOKOSC ~
    Q201=-7    ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q211=+0    ;PRZERWA CZAS. DNIE ~
    Q208=+90   ;POSUW RUCHU POWROTN. ~
    Q203=-9.5  ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS.
97 CALL LBL 1
98 M5 M9
99 ;
100 * - GW M8
101 TOOL CALL 27 Z S150
/ 102 STOP
103 TOOL DEF 3
104 M8
105 CYCL DEF 207 GWINTOWANIE GS  ~
    Q200=+4    ;BEZPIECZNA WYSOKOSC ~
    Q201=-19   ;GLEBOKOSC GWINTU ~
    Q239=+1.25 ;SKOK GWINTU ~
    Q203=-9.5  ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS.
106 CALL LBL 2
107 M5 M9
108 ;
109 * - GW 1/4
110 TOOL CALL 3 Z S120
/ 111 STOP
112 TOOL DEF 30
113 M8
114 CYCL DEF 207 GWINTOWANIE GS  ~
    Q200=+4    ;BEZPIECZNA WYSOKOSC ~
    Q201=-17   ;GLEBOKOSC GWINTU ~
    Q239=+1.337 ;SKOK GWINTU ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS.
115 CALL LBL 4
116 M5 M9
117 ;
118 ;---------------------------------------------
119 L  Z-5 R0 FMAX M91
120 L  X+460  Y+535 R0 FMAX M91
121 L M30
122 * - -----------------------------------------
123 * - LBL1 6H7
124 LBL 1
125 M3
126 L  X-13.23  Y+18.2 R0 FMAX M99
127 LBL 0
128 * - LBL2 GW M8
129 LBL 2
130 M3
131 L  X+0  Y+22.5 R0 FMAX M99
132 L  X+22.2  Y-3.9 R0 FMAX M99
133 L  X-22.2  Y-3.9 R0 FMAX M99
134 LBL 0
135 * - LBL3 6.6/13
136 LBL 3
137 M3
138 L  X+0  Y-50 R0 FMAX M99
139 L  X-47.6  Y-15.5 R0 FMAX M99
140 L  X-29.4  Y+40.5 R0 FMAX M99
141 L  X+29.4  Y+40.5 R0 FMAX M99
142 L  X+47.6  Y-15.5 R0 FMAX M99
143 LBL 0
144 * - LBL4 GW 1/4
145 LBL 4
146 M3
147 L  X-37.5  Y-35 R0 FMAX M99
148 L  X-43.3  Y+25 R0 FMAX M99
149 L  X+43.3  Y+25 R0 FMAX M99
150 L  X+37.5  Y-35 R0 FMAX M99
151 LBL 0
152 END PGM KR_N10103-1STR MM 
