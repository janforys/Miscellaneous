0  BEGIN PGM M8X075 MM 
1  BLK FORM 0.1 Z  X-60  Y-34  Z-119
2  BLK FORM 0.2  X+60  Y+0  Z+0
3  ;---------------------------------------------
4  CYCL DEF 247 USTAWIENIE PKT.BAZ ~
    Q339=+1    ;NR PKT BAZOWEGO
5  TOOL CALL 30 Z S50
6  TOOL DEF 9
7  TCH PROBE 409 PKT BAZ.SR.MOSTKA ~
    Q321=+0    ;SRODEK W 1-SZEJ OSI ~
    Q322=+0    ;SRODEK W 2-SZEJ OSI ~
    Q311=+75   ;SZEROKOSC MOSTKA ~
    Q272=+1    ;OS POMIAROWA ~
    Q261=-30   ;WYSOKOSC POMIARU ~
    Q320=+10   ;BEZPIECZNA WYSOKOSC ~
    Q260=+30   ;BEZPIECZNA WYSOKOSC ~
    Q305=+1    ;NR W TABELI ~
    Q405=+0    ;PUNKT ODNIESIENIA ~
    Q303=+1    ;PRZEKAZ DANYCH POM. ~
    Q381=+0    ;PROBKOW. NA OSI TS ~
    Q382=+0    ;1.WSPOL. DLA OSI TS ~
    Q383=+0    ;2.WSPOLRZ.DLA OSI TS ~
    Q384=+0    ;3. WSPOL. DLA OSI TS ~
    Q333=+0    ;PUNKT ODNIESIENIA
8  CYCL DEF 247 USTAWIENIE PKT.BAZ ~
    Q339=+1    ;NR PKT BAZOWEGO
9  CYCL DEF 7.0 PUNKT BAZOWY
10 CYCL DEF 7.1 IZ+0
11 ;---------------------------------------------
12 ;
13 ;
14 * - NAW
15 TOOL CALL 9 Z S1111 F111
16 TOOL DEF 8
17 M8
18 CYCL DEF 200 WIERCENIE ~
    Q200=+1    ;BEZPIECZNA WYSOKOSC ~
    Q201=-2    ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q202=+2    ;GLEBOKOSC DOSUWU ~
    Q210=+0    ;PRZER. CZAS.NA GORZE ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q211=+0    ;PRZERWA CZAS. DNIE ~
    Q395=+0    ;REFERENCJA GLEB.
19 CALL LBL 1
20 M5 M9
21 ;
22 * - W 6
23 TOOL CALL 8 Z S1300 F111
24 TOOL DEF 23
25 M8
26 CYCL DEF 200 WIERCENIE ~
    Q200=+2    ;BEZPIECZNA WYSOKOSC ~
    Q201=-44   ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q202=+3    ;GLEBOKOSC DOSUWU ~
    Q210=+0    ;PRZER. CZAS.NA GORZE ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q211=+0    ;PRZERWA CZAS. DNIE ~
    Q395=+0    ;REFERENCJA GLEB.
27 CALL LBL 1
28 M5 M9
29 ;
30 * - FI 10 
31 TOOL CALL 23 Z S7000 F500
32 TOOL DEF 1
33 M8
34 CYCL DEF 208 SPIRALNE FREZ. OTW. ~
    Q200=+1    ;BEZPIECZNA WYSOKOSC ~
    Q201=-1    ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q334=+0.4  ;GLEBOKOSC DOSUWU ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q335=+10   ;SREDNICA NOMINALNA ~
    Q342=+6    ;WYW.WSTEP. SREDNICA ~
    Q351=+1    ;RODZAJ FREZOWANIA
35 CALL LBL 1
36 CYCL DEF 208 SPIRALNE FREZ. OTW. ~
    Q200=+1    ;BEZPIECZNA WYSOKOSC ~
    Q201=-15   ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q334=+0.2  ;GLEBOKOSC DOSUWU ~
    Q203=-1    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+60   ;2-GA BEZPIECZNA WYS. ~
    Q335=+7.2  ;SREDNICA NOMINALNA ~
    Q342=+6    ;WYW.WSTEP. SREDNICA ~
    Q351=+1    ;RODZAJ FREZOWANIA
37 CALL LBL 1
38 M5 M9
39 ;
40 * - GW M8X075
41 TOOL CALL 1 Z S150
42 TOOL DEF 30
43 M8
44 CYCL DEF 207 GWINTOWANIE GS  ~
    Q200=+4    ;BEZPIECZNA WYSOKOSC ~
    Q201=-13   ;GLEBOKOSC GWINTU ~
    Q239=+0.75 ;SKOK GWINTU ~
    Q203=-1    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS.
45 CALL LBL 1
46 M5 M9
47 ;
48 ;
49 ;---------------------------------------------
50 L  Z-5 R0 FMAX M91
51 L  X+260  Y+535 R0 FMAX M91
52 L M30
53 * - -----------------------------------------
54 * - LBL1 O
55 LBL 1
56 M3
57 L  X+0  Y+0 R0 FMAX M99
58 LBL 0
59 END PGM M8X075 MM 
