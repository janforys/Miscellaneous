0  BEGIN PGM KS-N060_01-PIERSCIEN-UST MM 
1  BLK FORM 0.1 Z  X-62.5  Y-62.5  Z-15
2  BLK FORM 0.2  X+62.5  Y+62.5  Z+0
3  ;
4  * - UWAGA! SZCZEKI USTAWIC PO SKOSIE, SONDA PORUSZA SIE W SRODKU MIEDZY~
 SZCZEKAMI
5  ;
6  ;---------------------------------------------
7  CYCL DEF 247 USTAWIENIE PKT.BAZ ~
    Q339=+1    ;NR PKT BAZOWEGO
8  ;
/ 9  STOP
10 L  X+0  Y+0 FMAX
11 ;
12 TOOL CALL 30 Z
13 TOOL DEF 9
14 ;
15 TCH PROBE 412 PKT.BAZ.OKRAG WEWN. ~
    Q321=+0    ;SRODEK W 1-SZEJ OSI ~
    Q322=+0    ;SRODEK W 2-SZEJ OSI ~
    Q262=+72   ;SREDNICA NOMINALNA ~
    Q325=+0    ;KAT POCZATKOWY ~
    Q247=+90   ;KATOWY PRZYROST-KROK ~
    Q261=-4    ;WYSOKOSC POMIARU ~
    Q320=+26   ;BEZPIECZNA WYSOKOSC ~
    Q260=+20   ;BEZPIECZNA WYSOKOSC ~
    Q301=+0    ;ODJAZD NA BEZP.WYS. ~
    Q305=+1    ;NR W TABELI ~
    Q331=+0    ;PUNKT ODNIESIENIA ~
    Q332=+0    ;PUNKT ODNIESIENIA ~
    Q303=+1    ;PRZEKAZ DANYCH POM. ~
    Q381=+0    ;PROBKOW. NA OSI TS ~
    Q382=+0    ;1.WSPOL. DLA OSI TS ~
    Q383=+0    ;2.WSPOLRZ.DLA OSI TS ~
    Q384=+0    ;3. WSPOL. DLA OSI TS ~
    Q333=+0    ;PUNKT ODNIESIENIA ~
    Q423=+4    ;LICZBA PKT POMIAR. ~
    Q365=+1    ;RODZAJ PRZEMIESZCZ.
16 ;
17 CYCL DEF 247 USTAWIENIE PKT.BAZ ~
    Q339=+1    ;NR PKT BAZOWEGO
18 ;---------------------------------------------
19 ;
20 * - NAW
21 TOOL CALL 9 Z S1800 F100
/ 22 STOP
23 TOOL DEF 13
24 M8
25 CYCL DEF 200 WIERCENIE ~
    Q200=+2    ;BEZPIECZNA WYSOKOSC ~
    Q201=-3.6  ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q202=+3.6  ;GLEBOKOSC DOSUWU ~
    Q210=+0    ;PRZER. CZAS.NA GORZE ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q211=+0    ;PRZERWA CZAS. DNIE ~
    Q395=+0    ;REFERENCJA GLEB.
26 CALL LBL 1
27 M5 M9
28 ;
29 * - W 7
30 TOOL CALL 13 Z S1500 F150
/ 31 STOP
32 TOOL DEF 27
33 M8
34 CYCL DEF 200 WIERCENIE ~
    Q200=+2    ;BEZPIECZNA WYSOKOSC ~
    Q201=-19.5 ;GLEBOKOSC ~
    Q206= AUTO ;WARTOSC POSUWU WGL. ~
    Q202=+4    ;GLEBOKOSC DOSUWU ~
    Q210=+0    ;PRZER. CZAS.NA GORZE ~
    Q203=+0    ;WSPOLRZEDNE POWIERZ. ~
    Q204=+50   ;2-GA BEZPIECZNA WYS. ~
    Q211=+0    ;PRZERWA CZAS. DNIE ~
    Q395=+0    ;REFERENCJA GLEB.
35 CALL LBL 1
36 M5 M9
37 ;
38 * - FP-8
39 TOOL CALL 27 Z S5000 F250
/ 40 STOP
41 TOOL DEF 9
42 M3
43 L  X+64  Y-25 FMAX
44 L  Z+10 FMAX
45 L  Z-16 F1000 M8
46 L  X+58  Y-25 RR F AUTO
47 L  Y+25 RR F AUTO
48 L  Z+20 R0 FMAX
49 ;
50 L  X-64  Y+25 FMAX
51 L  Z+10 FMAX
52 L  Z-16 F1000
53 L  X-58  Y+25 RR F AUTO
54 L  Y-25 RR F AUTO
55 L  Z+20 R0 FMAX
56 ;
57 M5 M9
58 ;
59 * - NAW 90ST R=0 / KOMPENSACJA FAZY W "DL" 
60 TOOL CALL 9 Z S2000 F200 DL-0.1
/ 61 STOP
62 TOOL DEF 30
63 M3
64 ;
65 L  X+60  Y-25 FMAX
66 L  Z+10 FMAX M8
67 L  Z-2.75 F500
68 L  Y+25 F AUTO
69 L  Z+20 R0 FMAX
70 ;
71 L  X-60  Y+25 FMAX
72 L  Z+10 FMAX
73 L  Z-2.75 F500
74 L  Y-25 F AUTO
75 L  Z+20 R0 FMAX
76 ;
77 M5 M9
78 ;
79 ;---------------------------------------------
80 L  Z+0 R0 FMAX M91
81 L  X+260  Y+535 R0 FMAX M91
82 L M30
83 * - -----------------------------------------
84 * - LBL1 7
85 LBL 1
86 M3
87 L  X-42.5  Y+0 R0 FMAX M99
88 L  X-13.1  Y+40.4 R0 FMAX M99
89 L  X+34.4  Y+25 R0 FMAX M99
90 L  X+34.4  Y-25 R0 FMAX M99
91 L  X+0  Y-42.5 R0 FMAX M99
92 L  X-13.1  Y-40.4 R0 FMAX M99
93 LBL 0
94 END PGM KS-N060_01-PIERSCIEN-UST MM 
